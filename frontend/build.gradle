plugins {
    id 'java'
    id "com.github.node-gradle.node" version "3.0.1"
}

apply plugin: 'base'

node {
    version = '12.19.0'
    download = true
}

/**
     * Cleans everything that is created by the node plugin, i.e. the node installation and the node_modules with
     * .gradle folder
*/
task cleanAll(type: Delete){
    delete "build"
    delete "node_modules"
    delete ".gradle"
}

clean.dependsOn(cleanAll)

/**
     * Packs project with yarn build command
 */
task bundle(type: YarnTask, dependsOn: yarn_install) {
    args = ['build']
}

/**
     * Runs project with yarn start
 */
task run(type: YarnTask) {
    args = ['start']
}


/**
     * Copies files from yarn build to jar resources when backend is building
 * */
task webjar(type: Jar) {
    from(fileTree('build')) {
        into 'META-INF/resources'
    }
}

assemble.dependsOn(bundle)
jar.dependsOn(bundle)
jar.finalizedBy('webjar')