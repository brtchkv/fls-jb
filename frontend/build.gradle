plugins {
    id 'java'
    id "com.github.node-gradle.node" version "3.0.1"
}

apply plugin: 'base'

node {
    version = '12.19.0'
    download = true
}

ext {
    mode = System.getProperty("env", "development")
    port = System.getProperty("port", "8080")
}

/**
 * Cleans everything that is created by the node plugin, i.e. build folder, node_modules and gradle dependencies
 */
task cleanAll(type: Delete) {
    delete "build"
    delete "node_modules"
    delete ".gradle"
}

clean.dependsOn(cleanAll)

/**
 * Packs project with yarn build command. Use -Denv to specify runtime environment, either production or development
 * By default, development env is used
 */
task bundle(type: YarnTask, dependsOn: yarn_install) {
    environment = ["API_ENV": mode]
    if (mode == "production") {
        args = ["build-prod"]
    } else {
        args = ["build-dev"]
    }
    doLast {
        println "Bundled ${mode} build"
    }
}

/**
 * Runs project with yarn start. Use -Denv to specify runtime environment, either production or development
 * By default, development env is used
 */
task run(type: YarnTask) {
    environment = ["API_ENV": mode]
    if (mode == "production") {
        args = ["start-prod"]
    } else {
        args = ["start-dev"]
    }
    doLast {
        println "Running ${mode} build"
    }
}

assemble.dependsOn(bundle)
jar.dependsOn(bundle)